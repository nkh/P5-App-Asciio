
use Cairo ;
use List::Util qw(min max) ;
use Image::ExifTool;

# import and export png with embedded asciio

register_import_export_handlers 
	(
	png => 
		{
		IMPORT => \&import_pnge ,
		EXPORT => \&export_png,
		},
	pnge => 
		{
		EXPORT => \&export_pnge,
		},
	) ;

# --------------------------------------------------------------------------------

sub export_png
{
my ($self, $elements_to_save, $file)  = @_ ;

return export_pnge(@_) if($self->{IMPORT_EXPORT_HANDLERS}{HANDLER_DATA}{IS_PNGE}) ;

if($self->{CREATE_BACKUP} && -e $file)
	{
	use File::Copy;
	copy($file,"$file.bak") or die "export_pod: Copy failed while making backup copy: $!" ;
	}

local $self->{BINDINGS_COMPLETION} = undef ;

my ($font_character_width, $font_character_height) = $self->get_character_size($self->{FONT_FAMILY}, $self->{FONT_BINDINGS_SIZE}) ;

my $width  = max( ( map { $_->{X} + 1 + $_->{EXTENTS}[2]} $elements_to_save->@* )) * $font_character_width ;
my $height = max( ( map { $_->{Y} + 1 + $_->{EXTENTS}[3]} $elements_to_save->@* )) * $font_character_height ;

my $surface = Cairo::ImageSurface->create('argb32', $width, $height);
my $cr      = Cairo::Context->create($surface);

App::Asciio::GTK::Asciio::expose_event($self->{widget}, $cr, $self) ;

$surface->write_to_png("$file");

return $file ;
}

# --------------------------------------------------------------------------------

sub import_pnge
{
my ($self, $file)  = @_ ;

my $exifTool = new Image::ExifTool ;
$exifTool->ExtractInfo($file) ;

my $embedded_asciio = $exifTool->GetValue('Asciio') ;

if (defined $embedded_asciio) 
	{
	my $saved_self = $self->get_decompressed_asciio($embedded_asciio) ;
	
	if($@)
		{
		$self->{WARN}->("pnge: can't load file '$file': $! $@\n") ;
		}
	else
		{
		return($saved_self, $file, {IS_PNGE => 1}) ;
		}
	}
	
else
	{
	$self->{WARN}->("pnge: Tag 'asciio' not found\n") ;
	return ;
	}
}

# --------------------------------------------------------------------------------

sub export_pnge
{
my ($self, $elements_to_save, $file)  = @_ ;

my ($base_name, $path, $extension) = File::Basename::fileparse($file, ('\..*')) ;

local $self->{IMPORT_EXPORT_HANDLERS}{HANDLER_DATA}{IS_PNGE} = 0 ;

my $file_name = export_png
		(
		$self,
		$elements_to_save,
		$path . $base_name . '.png',
		) ;

my $exifTool = new Image::ExifTool ;

Image::ExifTool::AddUserDefinedTags
	(
	'Image::ExifTool::PNG::TextualData',
	asciio =>
		{
		Writable    => 'undef',
		Binary      => 1,
		Description => 'Custom Binary Chunk',
		},
	);

$exifTool->SetNewValue('Comment'=> 'generated by asciio') ;
$exifTool->SetNewValue('asciio' => $self->get_compressed_asciio);

$exifTool->WriteInfo($file_name);

return $file ;
}

