#!/usr/bin/env perl

use strict;
use warnings;
use utf8 ;

use Module::Util qw(find_installed) ;
use File::Basename ;
use Archive::Tar ;

use open qw( :std :encoding(UTF-8) ) ;
binmode(STDOUT) ;
binmode(STDIN) ;

use App::Asciio ;
my $ASCIIO_MIME_TYPE = "application/x-asciio" ;

# ------------------------------------------------------------------------------ 

asciio_to_text() ;

# ------------------------------------------------------------------------------ 

sub asciio_to_text
{
my ($asciio_config, $asciio) = get_asciio() ;

if (-p STDIN || -f STDIN) 
	{
	print STDERR "document_name: reading from STDIN\n" ;
	
	undef $/ ;
	$asciio->load_self($asciio->get_decompressed_asciio(<STDIN>)) ;
	
	print $asciio->transform_elements_to_ascii_buffer() ;
	}
else
	{
	for my $project_name ($asciio_config->{TARGETS}->@*)
		{
		process_file($asciio, $project_name) ;
		}
	
	# if(defined $asciio_config->{SCRIPT})
	# 	{
	# 	require App::Asciio::Scripting ;
			
	# 	App::Asciio::Scripting::run_external_script($asciio, $asciio_config->{SCRIPT}) ;
	# 	}
	}

}

# ------------------------------------------------------------------------------ 

sub process_file
{
my ($asciio, $project_name) = @_ ;

use Capture::Tiny qw/capture_merged/ ;

my $tar ;
my ($merged,  @result) = capture_merged
				{ 
				$tar = Archive::Tar->new($project_name) ; 
				} ;

if($tar)
	{
	my %documents = map { $_ => 1 } grep { $_ ne 'asciio_project' } $tar->list_files ;
	
	my $serialized_asciio_project = $tar->get_content('asciio_project') ;
	my $asciio_project            = eval { Sereal::Decoder->new->decode($serialized_asciio_project) } ;
	
	if ($@)
		{
		print STDERR "Error: deserializing '$project_name': $@" 
		}
	else
		{
		for my $document_name ($asciio_project->{documents}->@*) 
			{
			$asciio->load_serialized_self($tar->get_content($document_name)) ;
			print STDERR "document_name: $document_name\n" ;
			print $asciio->transform_elements_to_ascii_buffer() ;
			}
		}
	}
else
	{
	print STDERR "document_name: $project_name\n" ;
	
	my $document_name = $asciio->load_file($project_name) ;
	
	print $asciio->transform_elements_to_ascii_buffer() ;
	}
}

# ------------------------------------------------------------------------------ 

sub get_asciio
{
my $asciio = new App::Asciio() ;

$asciio->{UI} = 'TUI' ;

my ($command_line_switch_parse_ok, $command_line_parse_message, $asciio_config)
	= $asciio->ParseSwitches([@ARGV], 0) ;

die "Error: '$command_line_parse_message'!" unless $command_line_switch_parse_ok ;

my %object_override ; 
if(defined $asciio_config->{DEBUG_FD})
	{
	open my $fh, ">&=", $asciio_config->{DEBUG_FD} or die "can't open fd $asciio_config->{DEBUG_FD}: $!\n" ; 
	$fh->autoflush(1) ;
	%object_override = (WARN => sub { print $fh "@_\n" }, ACTION_VERBOSE => sub { print $fh "$_[0]\n" ; } ) ;
	}
else
	{
	%object_override = (WARN => sub { print STDERR "@_\n" }, ACTION_VERBOSE => sub { print STDERR "$_[0]\n" ; } ) ;
	}

my $setup_paths = [] ;

if(@{$asciio_config->{SETUP_PATHS}})
	{
	$asciio->setup($asciio_config->{SETUP_PATHS}, \%object_override) ;
	}
else
	{
	my ($basename, $path, $ext) = File::Basename::fileparse(find_installed('App::Asciio'), ('\..*')) ;
	my $setup_path = $path . $basename . '/setup/' ;
	
	$setup_paths = 
		[
		$setup_path . 'setup.ini', 
		$ENV{HOME} . '/.config/Asciio/Asciio.ini',
		] ;
		
	$asciio->setup($setup_paths, { %object_override, DISPLAY_SETUP_INFORMATION_ACTION => 0 }) ;
	}

return $asciio_config, $asciio ;
}

